:const BALLDATA_SIZE 8
:const BALLDATA_END 64
:const SHOULD_FILL_MASK_START 1
:const SHOULD_FILL_MASK_FINISH 2
:const DRAW_HORIZONTAL 1
:const DRAW_VERTICAL 2

### Change to 2 for hires
:const HIRES  1
:calc WIDTH { 0x40 * HIRES }
:calc XMAX { WIDTH - 1 }
:calc HEIGHT { 0x20 * HIRES }
:calc YMAX { HEIGHT - 1 }
:const SPEED HIRES
:calc WINSCORE { 7 * HIRES * HIRES } 


# General register strategy
# v0 - v7 often contain sets loaded from memory
# vf is vf
# ve is usually a loop offset, or some other extremely temporary thing.
# v8-vd are medium temporary, they might live between calls, holding some
#       bits of data from a different v0-v7 set, for example.


### Common register sets ###
# A set of data about one of the balls.
# Swapped in during the move calculations.
:alias bx v0
:alias by v1
:alias dx v2
:alias dy v3
:alias top v4
:alias left v5
:alias right v6
:alias bottom v7
:alias balldata v7
    

# Player data set
# Swapped in during movement key handling and line drawing.
:alias px v0
:alias py v1
:alias start v2
:alias finish v3
:alias direction v4
:alias start-target v5
:alias finish-target v6
:alias mark v7
:alias movedata v7

# Fill data
:alias fillstartx v0
:alias fillendx v1
:alias fillstarty v2
:alias fillendy v3
:alias fillshouldfill v4
:alias filldata v4

# Permanent
:alias offset ve # for looping over ballset
:alias sign vf

# moveset should be loaded
:macro draw-crosshair {
    i := crosshair
    sprite px py 2
    px -= 1
    py -= 1
    sprite px py 2
    px += 1
    py += 1
}

# moveset should be loaded
:macro move-crosshair coord amt bound {
    draw-crosshair
    coord += amt
    if coord == bound then coord -= amt 
    draw-crosshair
}

####################
### LINE DRAWING ###
####################

# Run on each loop to draw part of a player line
:macro check-lines {
    i := moveset
    load movedata
    if direction == 1 begin horizontal-step end
    if direction == 2 begin vertical-step end
    i := moveset
    save movedata
}


########################
### LINE ACTION INIT ###
########################

# Used by maybe-start-line-drawing to decide if the player is 
# In an unfilled region, and so could draw a line.
# For each active region, we check that px, py are full 
# in the bounds of that region.
# If they are, we save the line-drawing bounds, and the drawing
# direction into the movetargets.
# This includes setting the direction of moveset to check-direction,
# which will signal for the move to proceed.
:alias check-player-x vd
:alias check-player-y vc
:alias check-direction vb
: check-in-region
    offset := 0
    check-player-x := px
    check-player-y := py
    v0 := v0
    loop
        i := b1
        i += offset 
        load balldata

        if dx == 0 then return

        if check-player-x >= right then jump next-check
        if check-player-x <= left then jump next-check
        if check-player-y >= bottom then jump next-check
        if check-player-y <= top then jump next-check

        if check-direction == DRAW_HORIZONTAL begin
            v3 := left # left is v5, set before overwriting
            v0 := check-player-x 
            v1 := check-player-x
            v2 := check-direction 
            v4 := right
            v5 := check-player-y
        else
            v3 := top # top is v4, set before overwriting
            v0 := check-player-y
            v1 := check-player-y 
            v2 := check-direction 
            v4 := bottom
            v5 := check-player-x
        end
        i := movetargets
        save v5
        return

        : next-check
        offset += BALLDATA_SIZE
        while offset != BALLDATA_END
    again
;

# in check-direction = 1 - horizontal
#           2 - vertical
: maybe-start-line-drawing
    check-in-region
    i := moveset
    load movedata
    if direction == check-direction begin
        # undraw crosshair
        draw-crosshair

        # first dot for the player line
        i := dot
        sprite px py 1
    end
;


# Called from split-regions, assumes that the ball data
# set at b1 + offset was previously loaded, and will restore it.
:macro handle-region-split bound coord fillloc fillloc2 mask diff {
    # Save the provided bound to the provided location.
    # This is going to be one of the fill boundaries.
    v0 := bound
    i := fillloc
    save v0

    # Since we're drawing a line already, we don't need to account for it while
    # filling later.
    v0 := coord
    v0 -= diff
    i := fillloc2
    save v0

    # Save a mask that indicates the direction that we shrunk.
    i := filldatashouldfill
    load v0
    v1 := mask
    v0 |= v1
    save v0

    # Restore ball data.
    i := b1
    i += offset
    load v1
    bound := coord
    bound += diff
}

:alias splitting-x va
:alias splitting-y vd
:alias splitting-direction vc

# Called when a line-drawing sequence has finished (reached bounds on both sides
# This goes through each active ball region and determines if we have reduced the 
# size of this region. Resizes the region for each ball as appropriate. 

# As it's going, it also tracks whether a region shrink leaves any area with no 
# balls in it. This information is recorded in filldata.
: split-regions
    splitting-x := px
    splitting-y := py
    splitting-direction := direction
    offset := 0
    
    i := moveset
    save movedata

    # Account for the line in the score
    i := score
    load v1
    v2 := finish-target
    v2 -= start-target
    v0 += v2
    v1 += vf
    save v1
    v0 := v0

    # Set up the potential fill region 
    # (if there's a ball on only one side of the split)
    if direction == 1 begin
        v0 := start-target    # filldatastartx
        v1 := finish-target   # filldataendx
        v2 := splitting-y     # filldatastarty
        v3 := splitting-y     # filldataendy
    else 
        v0 := splitting-x     # filldatastartx
        v1 := splitting-x     # filldataendx
        v2 := start-target    # filldatastarty
        v3 := finish-target   # filldataendy
    end
    v4 := 0 # should fill
    i := fillset
    save filldata


    loop
        i := b1
        i += offset
        load balldata
        
        if dx == 0 then jump next-regions
        if splitting-x <= left then jump next-regions
        if splitting-x >= right then jump next-regions
        if splitting-y <= top then jump next-regions
        if splitting-y >= bottom then jump next-regions

        # splitting horizontal 
        if splitting-direction == DRAW_HORIZONTAL begin
            if by <= splitting-y begin
                if splitting-y < bottom begin 
                    handle-region-split bottom splitting-y filldataendy filldatastarty SHOULD_FILL_MASK_FINISH -1
                end
            end
            if by >= splitting-y begin
                if splitting-y > top begin 
                    handle-region-split top splitting-y filldatastarty filldataendy SHOULD_FILL_MASK_START 1
                end
            end
        end

        # splitting vertical
        if splitting-direction == DRAW_VERTICAL begin
            if bx <= splitting-x begin
                if splitting-x < right begin
                    handle-region-split right splitting-x filldataendx filldatastartx SHOULD_FILL_MASK_FINISH -1
                end
            end
            if bx >= splitting-x begin
                if splitting-x > left begin
                    handle-region-split left splitting-x filldatastartx filldataendx SHOULD_FILL_MASK_START 1
                end
            end
        end

        save balldata
        : next-regions
        offset += BALLDATA_SIZE
        while offset != BALLDATA_END
    again

    runfill
    
    i := moveset
    load movedata
;


:macro step coord target diff x y {
    if coord != target begin coord += diff sprite x y 1 steps-taken += 1 end
}

:macro steps x1 y1 x2 y2 {
    i := dot
    steps-taken := 0
    step start start-target -1 x1 y1 
    step start start-target -1 x1 y1 
    step start start-target -1 x1 y1 
    step finish finish-target 1 x2 y2
    step finish finish-target 1 x2 y2
    step finish finish-target 1 x2 y2
    if steps-taken == 0 begin
        draw-crosshair
        split-regions 
        direction := 0
    end
}

# Active set: moveset
:alias steps-taken ve
:macro horizontal-step {
    steps start py finish py
}

# Active set: moveset
:macro vertical-step {
    steps px start px finish
}

# Go through the balls until one isn't one, turn it on
:alias ball-was-active vd
:macro next-level {
    v0 := 0
    v1 := 0
    i := score
    save v1
    offset := 0
    loop
        i := b1
        i += offset
        load balldata
        ball-was-active := dx
        bx := random XMAX
        by := random YMAX 
        dx := random SPEED
        dx += SPEED
        dy := random SPEED
        dy += SPEED
        top := 0
        left := 0
        right := XMAX
        bottom := YMAX 
        save balldata
        i := dot
        sprite px py 1
        if ball-was-active == 0 begin
            offset := BALLDATA_END
        else
            offset += BALLDATA_SIZE
        end
        while offset != BALLDATA_END
    again
    i := moveset
    load movedata
}

# Clobbers v0-v5
:alias checking-key vd
:macro check-keys {
    i := moveset
    load movedata
    if direction == 0 begin
        checking-key := OCTO_KEY_W if checking-key key begin move-crosshair py -1 -1 end
        checking-key := OCTO_KEY_S if checking-key key begin move-crosshair py 1 HEIGHT end
        checking-key := OCTO_KEY_A if checking-key key begin move-crosshair px -1 -1 end
        checking-key := OCTO_KEY_D if checking-key key begin move-crosshair px 1 WIDTH end
        checking-key := OCTO_KEY_Q if checking-key key begin check-direction := 2 maybe-start-line-drawing end
        checking-key := OCTO_KEY_E if checking-key key begin check-direction := 1 maybe-start-line-drawing end
    end
    i := moveset
    save movedata
}

# Messes up:
# Fill in an area of the screen.
:alias fillx v4
:alias filly v5
:alias dotwidth v6
:alias score-add-lo v7
:alias score-add-hi v8
: runfill

    i := score
    load v1
    score-add-lo := v0
    score-add-hi := v1
    v0 := v0

    i := fillset
    load filldata

    # Split with ball to left/top sets 1
    # Split with ball to right/bottom sets 2
    # If neither bit is set we don't fill - although probably something went wrong in this case.
    # If both bits are set we don't fill - there's a ball on both sides still.
    if fillshouldfill == 0 then return
    if fillshouldfill == 3 then return

    fillendx += 1
    fillx := fillstartx
    filly := fillstarty
# Fill from left to right,
# in Top-to-bottoms stripes
    loop
        # Calculate the stripewidth. If there's a lot left to fill, it's 8.
        # Otherwise, it is the amount left to fill.
        dotwidth := fillendx 
        dotwidth -= fillx
        if dotwidth > 8 then dotwidth := 8

        # Get the graphic location corresponding to this vertical stripe.
        i := beforedots 
        i += dotwidth

        loop
            sprite fillx filly 1

            score-add-lo += dotwidth
            score-add-hi += vf

            while filly != fillendy
            filly += 1
        again

        # We hit the bottom, move over
        fillx += dotwidth
        filly := fillstarty

        while fillx < fillendx
    again

    if score-add-hi >= WINSCORE then jump level-clear 
    v0 := score-add-lo
    v1 := score-add-hi
    i := score
    save v1
    v0 := v0

;


############################
### BALL MOVING ROUTINES ###
############################


# v8-vb hold information needed to 
# determine if a ball is crossing
# a line that's being drawn.
# The ball data set is v0-v7, so
# we start at v8.
:alias movecalc-start v8
:alias movecalc-finish v9
:alias movecalc-direction va
:alias movecalc-mark vb
# Set for-each-ball-action to a valid call
# And then call this
: move-all 
    movecalc-start := start
    movecalc-finish := finish
    movecalc-direction := direction
    if direction == 1 then movecalc-mark := py
    if direction == 2 then movecalc-mark := px

    offset := 0
    loop
        i := b1
        i += offset
        load balldata
        if dx != 0 begin
            moveball
            i := b1
            i += offset
            save balldata
            offset += BALLDATA_SIZE
        else 
            offset := BALLDATA_END
        end
        while offset != BALLDATA_END
    again
;

### Boundary crossing notes:
# The math is simple, and we can use the same pattern for both directions, 
# but there's a lot to keep track of.
# Consider the x dimension:
# We are either moving left (decreasing) or moving right (increasing).
#
# First, we subtract the boundary from the coordinate, so that we're working
# relative to a boundary of 0.
#
# Now, we add the speed to the coordinate.
# 
# The movement results in a crossing based on the value of the vf carry flag. BUT, 
# the value expect depends on the direction of movement. If we were moving left,
# Then when we cross, we expect to *not* get the carry flag.
# For example:
#   x = 5, dx = -2, left = 4. 
#   We adjusted for the boundary, so x = 1.
#   x + dx ->   
#   1 + -2 -> 
#   0x01 + 0xFE -> 0xFF. Note that there was no carry!
#   In other words, adding -2 gives the same vf as subtracting 2. 
#
#   For a non cross:
#   x = 5 dx = -1 left = 3
#   x = 2 after bounds adjust.
#   x + dx -> 2 + -2 -> 0x02 + 0xFE -> 0x00 -- a carry!
#
#   We only check left bound when moving left and right bound when moving right, so 
#   there's no need for the math to work out on the opposite boundaries (and no need 
#  do those pointless checks).



### Single macro to handle bounds check.
# coord: the current value of the coordinate checked
# velo: the speed along the dimension
# bound: the bound we are checking against
# expect: the carry flag the indicates a hit (see notes above)
:macro checkbound coord velo bound hitwhencarryis {
    # do math to calculate a boundary crossing.
    coord -= bound
    coord += velo
    if vf == hitwhencarryis begin 
        # clip to bound
        coord := bound
        # negate velocity
        vf := 0
        velo =- vf
        # TODO - maybe random it up a little?
    else
        coord += bound
    end	
}

### Call the appropriate checkbound for a coordinate
:macro ud2 coordinate velocity boundStart boundEnd {
    # Find sign of velocity
    # Shifts left one, MSB goes into vF (sign)
    # We're ready for both shift behaviors!
    sign := velocity
    sign <<= velocity

    if sign == 1 begin
        # We are moving to the left, or up (decreasing coord)
        # So check for corssing the start/minimum boundary
        checkbound coordinate velocity boundStart 0
        else 
        # We are moving to the right, or down (increasing coord)
        # So check for corssing the end/maximum boundary
        checkbound coordinate velocity boundEnd 1
    end
}

: hmoveball
    if movecalc-direction != DRAW_VERTICAL then jump hjustdraw

    if by < movecalc-start then jump hjustdraw
    if by > movecalc-finish then jump hjustdraw

    if bx < movecalc-mark begin
        ud2 bx dx left right
        if bx >= movecalc-mark then jump lose
        return
    end

    if bx > movecalc-mark begin
        ud2 bx dx left right
        if bx <= movecalc-mark then jump lose
        return
    end
        
    : hjustdraw 
    ud2 bx dx left right
;

: vmoveball
    if movecalc-direction != DRAW_HORIZONTAL then jump vjustdraw

    if bx < movecalc-start then jump vjustdraw
    if bx > movecalc-finish then jump vjustdraw

    if by < movecalc-mark begin
        ud2 by dy top bottom
        if by >= movecalc-mark then jump lose
        return
    end

    if by > movecalc-mark begin
        ud2 by dy top bottom
        if by <= movecalc-mark then jump lose
        return
    end
        
    : vjustdraw 
    ud2 by dy top bottom
;

: moveball
    i := dot
    sprite bx by 1
    hmoveball
    vmoveball
    i := dot
    sprite bx by 1
;

: splode
    v8 := 5
    loop
        ve := v9
        vf := 1
        ve >>= vf

        v0 := va
        v0 -= ve
        v1 := vb
        va -= ve
        vf := delay
        if vf == 0 begin
            v2 := random 0xFF
            v3 := random 0xFF
            v4 := random 0xFF
            v2 &= v9
            v3 &= v9
            v0 += v2
            v1 += v3
            i := 0x200
            i += v4
            sprite v0 v1 8
            vf := 6
            delay := vf
            v8 -= 1
            while v8 != 0
        end
    again
;

: lose
    va := bx
    vb := by
    v9 := 3
    splode
    v9 := 7
    splode
    v9 := 15 
    splode
    v9 := 31 
    splode
    v9 := 63 
    splode
    v0 := key
    loop 
        while v0 key
    again
    jump restart





: level-clear
    clear
    i := level
    load v0
    v0 += 1
    save v0
    i := hex v0
    sprite v0 v0 5
    v0 := key
    loop
        while v0 key
    again
    clear
    jump playstart 
;


:macro letter x y name {
    v0 := x v1 := y
    i := name
    sprite v0 v1 11
}

:macro init-ball addr {
    i := addr
    load v4
    i := dot
    sprite bx by 1
}

:macro clear-ball addr {
    i := addr
    load v4
    dx := 0
    save v4
}

:calc TC1X { 14 * HIRES }
:calc TC1Y { 4 * HIRES }
:calc TL1X { 23 * HIRES }
:calc TL1Y { 4 * HIRES }
:calc TO1X { 32 * HIRES }
:calc TO1Y { 4 * HIRES }
:calc TS1X { 41 * HIRES }
:calc TS1Y { 4 * HIRES }

:calc TT1X { 18 * HIRES }
:calc TT1Y { 17 * HIRES }
:calc TR1X { 27 * HIRES }
:calc TR1Y { 17 * HIRES }
:calc TO2X { 36 * HIRES }
:calc TO2Y { 17 * HIRES }

: title-screen
    clear

    letter TC1X TC1Y title-c
    letter TL1X TL1Y title-l
    letter TO1X TO1Y title-o
    letter TS1X TS1Y title-s

    letter TT1X TT1Y title-t
    letter TR1X TR1Y title-r
    letter TO2X TO2Y title-o

    init-ball b1
    init-ball b2
    init-ball b3

    loop
        vf := delay
        if vf == 0 begin
            move-all
            vf := 4
            delay := vf
        end
        v0 := 0
        loop 
            if v0 key then jump done
            v0 += 1
            while v0 != 0x10
        again
            
    again
    : done
    loop 
        while v0 key
    again

    clear-ball b1
    clear-ball b2
    clear-ball b3
    clear-ball b4
    clear-ball b5
    clear-ball b6
    clear-ball b7
    clear-ball b8
    clear
;

: title-c
 0xFE
 0x80
 0x80
 0x80
 0x80
 0x80
 0x80
 0x80
 0x80
 0x80
 0xFE

: title-l
 0x80
 0x80
 0x80
 0x80
 0x80
 0x80
 0x80
 0x80
 0x80
 0x80
 0xFE

: title-o
 0xFE
 0x82
 0x82
 0x82
 0x82
 0x82
 0x82
 0x82
 0x82
 0x82
 0xFE

: title-s
 0xFE
 0x80
 0x80
 0x80
 0x80
 0xFE
 0x02
 0x02
 0x02
 0x02
 0xFE

: title-t
 0xFE
 0x10
 0x10
 0x10
 0x10
 0x10
 0x10
 0x10
 0x10
 0x10
 0x10

: title-r
 0xFE
 0x82
 0x82
 0x82
 0x82
 0xFE
 0x88
 0x88
 0x88
 0x88
 0x88


# repack as 
# x dx l r y dy t b
# So we can do smaller loads
#  x y dx dy t l r b
# dx/dy == 0 signals ball not yet active
# and we assume that all subsequence aren't either
: b1
    33 6 1 3 5 33 37 13
: b2
    37 18 2 1 18 37 41 26
: b3
    15 5 1 1 5 15 22 13
: b4
    0 0 0 0 0 0 63 31
: b5
    0 0 0 0 0 0 63 31
: b6
    0 0 0 0 0 0 63 31
: b7
    0 0 0 0 0 0 63 31
: b8
    0 0 0 0 0 0 63 31
: b-end

: moveset
# px py 
  10 10  
: movetargets
# start end 
  0     0   
# direction(1=h, 2=v) 
  0
# start-target end-target
  0    0
# mark
  0

: score
  0 0

: level
  0

: fillset
# x bounds for fill
: filldatastartx
  0 
: filldataendx
  0
# y bounds for fill
: filldatastarty
  0
: filldataendy
  0
: filldatashouldfill
  0

: beforedots 0xFF
# Various width "dots" for the remainder part of the fill routine.
: dot 0x80 
: dot2 0xC0 
: dot3 0xE0
: dot4 0xF0
: dot5 0xF8
: dot6 0xFC
: dot7 0xFE
: dot8 0xFF
: data

# .X.
# X.X
# .X.
#
# As .X
#    X.
# drawn twice.

: crosshair
            0x40
            0x80
: ball
            0x80

:alias delay-check ve
: main
    v0 := WIDTH
    if v0 == 0x80 then hires
: restart
    title-screen

: playstart
    px := 0x20
    py := 0x10
    direction := 0
    draw-crosshair
    i := moveset
    save movedata
    next-level

    loop
        check-keys

        delay-check := delay
        if delay-check == 0 begin
            check-lines
            move-all
            delay-check := 3
            delay := delay-check
        end

    again


